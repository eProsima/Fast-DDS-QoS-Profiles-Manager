# Copyright 2023 Proyectos y Sistemas de Mantenimiento SL (eProsima).
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

set(@PROJECT_NAME@_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

set_and_check(@PROJECT_NAME@_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(@PROJECT_NAME@_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")
set_and_check(@PROJECT_NAME@_DOC_DIR "@PACKAGE_DOC_INSTALL_DIR@")
set_and_check(@PROJECT_NAME@_HTML_DIR "@PACKAGE_HTML_INSTALL_DIR@")
set_and_check(@PROJECT_NAME@_XML_DIR "@PACKAGE_XML_INSTALL_DIR@")

set(fastdds_qos_profiles_manager_lib_known_comp static shared)
set(fastdds_qos_profiles_manager_lib_comp_static NO)
set(fastdds_qos_profiles_manager_lib_comp_shared NO)
foreach (fastdds_qos_profiles_manager_lib_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (fastdds_qos_profiles_manager_lib_comp IN_LIST fastdds_qos_profiles_manager_lib_known_comp)
        set(fastdds_qos_profiles_manager_lib_comp_${fastdds_qos_profiles_manager_lib_comp} YES)
    else()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "fastdds_qos_profiles_manager_lib does not recognize component `${fastdds_qos_profiles_manager_lib_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif()
endforeach()

if (fastdds_qos_profiles_manager_lib_comp_static AND fastdds_qos_profiles_manager_lib_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "fastdds_qos_profiles_manager_lib `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif()

set(fastdds_qos_profiles_manager_lib_static_targets
    "${CMAKE_CURRENT_LIST_DIR}/fastdds_qos_profiles_manager_lib-static-targets.cmake")
set(fastdds_qos_profiles_manager_lib_shared_targets
    "${CMAKE_CURRENT_LIST_DIR}/fastdds_qos_profiles_manager_lib-shared-targets.cmake")

macro(fastdds_qos_profiles_manager_lib_load_targets type)
    if (NOT EXISTS "${fastdds_qos_profiles_manager_lib_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "fastdds_qos_profiles_manager_lib `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif()
    include("${fastdds_qos_profiles_manager_lib_${type}_targets}")
endmacro()

if (fastdds_qos_profiles_manager_lib_comp_static)
    fastdds_qos_profiles_manager_lib_load_targets(static)
elseif (fastdds_qos_profiles_manager_lib_comp_shared)
    fastdds_qos_profiles_manager_lib_load_targets(shared)
elseif (DEFINED fastdds_qos_profiles_manager_lib_SHARED_LIBS AND fastdds_qos_profiles_manager_lib_SHARED_LIBS)
    fastdds_qos_profiles_manager_lib_load_targets(shared)
elseif (DEFINED fastdds_qos_profiles_manager_lib_SHARED_LIBS AND NOT fastdds_qos_profiles_manager_lib_SHARED_LIBS)
    fastdds_qos_profiles_manager_lib_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${fastdds_qos_profiles_manager_lib_shared_targets}")
        fastdds_qos_profiles_manager_lib_load_targets(shared)
    else()
        fastdds_qos_profiles_manager_lib_load_targets(static)
    endif()
else()
    if (EXISTS "${fastdds_qos_profiles_manager_lib_static_targets}")
        fastdds_qos_profiles_manager_lib_load_targets(static)
    else()
        fastdds_qos_profiles_manager_lib_load_targets(shared)
    endif()
endif()
